plugins {
    id 'java'
    id 'idea'
}

group 'com.techcourse'
version '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':mvc')

    implementation 'org.apache.tomcat.embed:tomcat-embed-core:11.0.10'
    implementation 'org.apache.tomcat.embed:tomcat-embed-jasper:11.0.10'
    implementation 'ch.qos.logback:logback-classic:1.5.18'
    implementation 'org.apache.commons:commons-lang3:3.18.0'

    testImplementation 'org.assertj:assertj-core:3.27.3'
    testImplementation 'org.mockito:mockito-core:5.19.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.13.4'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.13.4'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.13.4'
}

/** ---------- 추가: mvc 산출물(JAR)을 WEB-INF/lib로 복사 ---------- **/
configurations {
    mvcJar
}

dependencies {
    mvcJar project(path: ':mvc', configuration: 'default')
}

tasks.register('copyMvcJarToWebInfLib', Copy) {
    dependsOn configurations.mvcJar
    from(configurations.mvcJar)
    into("$projectDir/src/main/webapp/WEB-INF/lib")
}

tasks.named('classes') {
    finalizedBy('copyMvcJarToWebInfLib')
}
/** ----------------------------------------------------------------- **/

test {
    useJUnitPlatform()
}

idea {
    module {
        inheritOutputDirs = false
        outputDir file('src/main/webapp/WEB-INF/classes')
    }
}

sourceSets {
    main {
        java.destinationDirectory.set(file('src/main/webapp/WEB-INF/classes'))
    }
}
